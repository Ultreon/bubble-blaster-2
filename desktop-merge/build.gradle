//file:noinspection GroovyUnusedCatchParameter
//file:noinspection JavaPluginLanguageLevel


import org.panteleyev.jpackage.ImageType
import org.panteleyev.jpackage.JPackageTask

import java.nio.file.Files
import java.nio.file.Paths

buildscript {
    repositories {
        mavenCentral()

        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
            name = "sonatype"
        }

        maven {
            url = "https://maven.atlassian.com/3rdparty/"
        }

        google()
    }

    dependencies {
        classpath group: 'commons-io', name: 'commons-io', version: "$commons_io_version"
        classpath "com.google.code.gson:gson:2.10.1"
    }
}

//*****************//
//     Plugins     //
//*****************//
plugins {
    //noinspection JavaPluginLanguageLevel
    id "java"
    //noinspection JavaPluginLanguageLevel
    id "java-library"
    id "org.panteleyev.jpackageplugin" version "1.5.0"
}

apply plugin: 'maven-publish'

evaluationDependsOn ":"

//****************************//
// Setting up main properties //
//****************************//

// Project properties.
group project_group

def packageVersion = "1.0.0"

java {
    withSourcesJar()
    withJavadocJar()
}

repositories {
    maven {
        url = "https://maven.fabricmc.net"
        name = "FabricMC"
    }
}

configurations {
    addToJar {
        canBeResolved true
    }
}

dependencies {
    addToJar(project(":gameprovider"))
    pack implementation(project(":core"))
    pack implementation(project(":desktop"))

    pack implementation("com.badlogicgames.gdx:gdx:$gdx_version")
    pack implementation("com.badlogicgames.gdx:gdx-box2d:$gdx_version")
    pack implementation("com.badlogicgames.ashley:ashley:$ashley_version")
    pack implementation("com.badlogicgames.gdx:gdx-ai:$ai_version")
    pack implementation("com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdx_controllers_version")
    pack implementation("com.badlogicgames.gdx:gdx-freetype:$gdx_version")
    pack implementation("com.badlogicgames.box2dlights:box2dlights:$box_2d_lights_version")

    pack implementation("com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdx_version")
    pack implementation("com.badlogicgames.gdx:gdx-platform:$gdx_version:natives-desktop")
    pack implementation("com.badlogicgames.gdx:gdx-box2d-platform:$gdx_version:natives-desktop")
    pack implementation("com.badlogicgames.gdx:gdx-bullet-platform:$gdx_version:natives-desktop")
    pack implementation("com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdx_controllers_version")
    pack implementation("com.badlogicgames.gdx:gdx-freetype-platform:$gdx_version:natives-desktop")

    // Apache Log4J
    pack annotationProcessor("org.apache.logging.log4j:log4j-core:$log4j_version")
    pack implementation("org.apache.logging.log4j:log4j:$log4j_version")
    pack implementation("org.apache.logging.log4j:log4j-api:$log4j_version")
    pack implementation("org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version")

    // JNA
    pack implementation("net.java.dev.jna:jna:$jna_version")
    pack implementation("net.java.dev.jna:jna-platform:$jna_version")

    // SLF4J
    pack implementation("org.slf4j:slf4j-api:$slf4j_version")

    // Google
    pack implementation("com.google.code.gson:gson:2.10.1")
    pack implementation('com.google.guava:guava:32.0.0-jre')

    // Misc
    pack implementation('it.unimi.dsi:fastutil:8.5.12')

    pack implementation("space.earlygrey:shapedrawer:$shapedrawer_version")

    // ImGui
    pack implementation("io.github.spair:imgui-java-binding:$imgui_version")
    pack implementation("io.github.spair:imgui-java-lwjgl3:$imgui_version")
    pack implementation("io.github.spair:imgui-java-natives-linux:$imgui_version")
    pack implementation("io.github.spair:imgui-java-natives-macos:$imgui_version")
    pack implementation("io.github.spair:imgui-java-natives-windows:$imgui_version")

    pack implementation("it.unimi.dsi:fastutil-core:$fastutil_version")
    pack implementation("com.google.code.findbugs:jsr305:$jsr_version")
    pack testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
    pack testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")

    pack implementation("org.xbib.elasticsearch:joptsimple:6.3.2.1")

    // fabric-loader dependencies
    pack implementation("org.ow2.asm:asm:${project.asm_version}")
    pack implementation("org.ow2.asm:asm-analysis:${project.asm_version}")
    pack implementation("org.ow2.asm:asm-commons:${project.asm_version}")
    pack implementation("org.ow2.asm:asm-tree:${project.asm_version}")
    pack implementation("org.ow2.asm:asm-util:${project.asm_version}")

    pack implementation("net.fabricmc:sponge-mixin:${project.mixin_version}") {
        exclude(module: 'launchwrapper')
        exclude(module: 'guava')
        exclude(module: 'gson')
    }

    //noinspection GradleDynamicVersion
    pack implementation('net.fabricmc:tiny-mappings-parser:0.3.0+build.17')
    pack implementation('net.fabricmc:tiny-remapper:0.8.7')
    //noinspection GradleDynamicVersion
    pack implementation('net.fabricmc:dev-launch-injector:0.2.1+build.8')
    pack implementation('net.fabricmc:access-widener:2.1.0')

    pack compileOnly("net.fabricmc:fabric-loader:$fabric_version")
}

processResources {
    from(file("$rootProject.projectDir/LICENSE")) { into "META-INF/" }
    exclude("**/*.pdn")
    exclude("**/*.xcf")
    exclude("**/*.ps")
    exclude("**/*.tmp")
    exclude("**/*.bak")
    exclude("**/*.temp")
    exclude("**/*.cache")
    exclude("**/.cache")

    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

jar {
    dependsOn ":gameprovider:build"
    dependsOn ":desktop:build"
    dependsOn ":core:build"
    //noinspection GroovyAssignabilityCheck
    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes 'Implementation-Title': 'Bubble Blaster',
                'Implementation-Vendor': 'Ultreon Team',
                project_version: project.project_version,
                'Main-Class': 'dev.ultreon.craft.DesktopLauncher',
                'Multi-Release': 'true'
    }

    from(configurations.addToJar.collect {
        zipTree(it).matching {
            exclude "*.RSA", "*.SF", "*.MF"
        }
    })
    from {
        compileJava.outputs
    }
    from {
        processResources.outputs
    }

    exclude "*.RSA", "*.SF", "*.MF"

    zip64 true
    duplicatesStrategy DuplicatesStrategy.INCLUDE

    doLast {
        println "DESK_MERGE FILE: ${file("$buildDir/libs/").list().toArrayString()}"
        println "CORE FILE: ${file("${project(":core") buildDir}/libs/").list().toArrayString()}"
        println "DESKTOP FILE: ${file("${project(":desktop") buildDir}/libs/").list().toArrayString()}"
        println "GAMEPROVIDER FILE: ${file("${project(":gameprovider") buildDir}/libs/").list().toArrayString()}"
    }
}

tasks.register('prepareRun', {
    Files.createDirectories(Paths.get(rootProject.projectDir.getAbsolutePath(), "run"))
})

tasks.withType(JavaCompile).configureEach {
    options.fork = true
    options.incremental = true
}

tasks.withType(ProcessResources).configureEach {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

delete "$buildDir/jars"

tasks.register('copyDependencies', Copy) {
    from(configurations.runtimeClasspath)
    exclude "lwjgl-2.9.3.jar"
    exclude "lwjgl-platform-2.9.3.jar"
    exclude "lwjgl-platform-2.9.3-*.jar"
    into("$buildDir/jars")
}

tasks.register('copyJar', Copy) {
    from(tasks.jar)
    into("$buildDir/jars")
}

tasks.register('package', Zip) {
    dependsOn("build", ":gameprovider:build", ":desktop:build", ":core:build", "copyDependencies", "copyJar")

    from fileTree(dir: "$buildDir/jars", include: "*.jar")
    archiveName 'package.zip'
    destinationDir(file("$rootProject.buildDir/dist/"))
}

final vmArgs = ["-Dfile.encoding=UTF-8", "-Dfabric.skipMcProvider=true", "-Xmx4G", "-Xms256M",
                "-Dbubbles.packaged=true", "-Dbubbles.packageType=jpackage", "-Dbubbles.version=${project.version}"
]
jpackage {
    dependsOn("build", ":gameprovider:build", ":desktop:build", ":core:build", "copyDependencies", "copyJar")

    inputs.files(fileTree("$buildDir/jars"))

    input = "$buildDir/jars"
    destination = "$buildDir/bin"

    appName = project.property("app_name").toString()
    appVersion = project.version.toString()
    vendor = "Ultreon Team"
    copyright = "Copyright (c) 2023 Ultreon Team"
    runtimeImage = System.getProperty("java.home")

//    mainJar = "fabric-loader-${fabric_loader_version}.jar"
    mainJar = jar.archiveFileName.get()
    mainClass = "dev.ultreon.bubbles.premain.PreMain"

    destination = "$buildDir/dist"

    licenseFile = "$rootProject.projectDir/package/LICENSE.txt"

    javaOptions = vmArgs
    arguments = ["--packaged"]

    aboutUrl = "https://github.com/Ultreon/bubble-blaster"

    mac {
        icon = "$rootProject.projectDir/icons/icon.icns"
        macPackageIdentifier = project_id
        macPackageName = project_name
        appVersion = packageVersion
        javaOptions = javaOptions + ["-XstartOnFirstThread", "-Dbubbles.packageFormat=dmg"]
    }

    linux {
        icon = "$rootProject.projectDir/icons/icon.png"
        linuxPackageName = project_name
        linuxDebMaintainer = "Ultreon Team"
        linuxRpmLicenseType = "Ultreon API License v1.1"
        linuxAppRelease = "2"
        linuxShortcut = true
        javaOptions = javaOptions + ["-Dbubbles.packageFormat=deb"]
        appVersion = project.version.toString()
    }

    windows {
        icon = "$rootProject.projectDir/icons/icon.ico"
        winMenu = true
        winDirChooser = true
        winConsole = false
        winPerUserInstall = true
        winShortcutPrompt = true
        winShortcut = false
        winUpgradeUuid = "a539b496-ae50-4df2-9a1b-8a089e44f7ce"
        winMenuGroup = "Ultreon Team"
        winConsole = false
        javaOptions = javaOptions + ["-Dbubbles.packageFormat=exe"]
        appVersion = (project.version).toString().replaceAll("-cb\\.\\d+", "").replace("+", ".")
        type = ImageType.MSI
    }
}

tasks.register('jpackageAlt', JPackageTask) {
    dependsOn("build", ":gameprovider:build", ":desktop:build", ":core:build", "copyDependencies", "copyJar")

    inputs.files(fileTree("$buildDir/jars"))

    input = "$buildDir/jars"
    destination = "$buildDir/bin"

    appName = project.property("app_name").toString()
    appVersion = project.version.toString()
    vendor = "Ultreon Team"
    copyright = "Copyright (c) 2023 Ultreon Team"
    runtimeImage = System.getProperty("java.home")

    mainJar = jar.archiveFileName.get()
    mainClass = "dev.ultreon.bubbles.premain.PreMain"

    destination = "$buildDir/dist"

    javaOptions = vmArgs
    arguments = ["--packaged"]

    mac {
        icon = "$rootProject.projectDir/icons/icon.icns"
        macPackageIdentifier = project_id
        macPackageName = project_name
        appVersion = packageVersion
        javaOptions = javaOptions + ["-XstartOnFirstThread", "-Dbubbles.packageFormat=appimage"]
        type = ImageType.APP_IMAGE
    }

    linux {
        icon = "$rootProject.projectDir/icons/icon.png"
        appVersion = project.version.toString()
        javaOptions = javaOptions + ["-Dbubbles.packageFormat=appimage"]
        type = ImageType.APP_IMAGE
    }

    windows {
        icon = "$rootProject.projectDir/icons/icon.ico"
        winConsole = false
        appVersion = (project.version).toString().replaceAll("-cb\\.\\d+", "").replace("+", ".")
        javaOptions = javaOptions + ["-Dbubbles.packageFormat=appimage"]
        type = ImageType.APP_IMAGE
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = "bubble-blaster-" + project.name
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Ultreon/bubble-blaster-2")
            credentials {
                username = project.findProperty("gpr.user") == null ? System.getenv("USERNAME") : project.findProperty("gpr.user")
                password = project.findProperty("gpr.key") == null ? System.getenv("TOKEN") : project.findProperty("gpr.key")
            }
        }
    }
}
