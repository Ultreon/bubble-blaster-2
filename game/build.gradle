//file:noinspection GroovyUnusedCatchParameter

import java.nio.file.Files
import java.nio.file.Paths
import com.ultreon.bubbles.gradle.VersionType

import java.nio.file.StandardOpenOption

import static java.lang.System.getenv

buildscript {
    repositories {
        mavenCentral()

        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
            name = "sonatype"
        }

        maven {
            url = "https://maven.atlassian.com/3rdparty/"
        }

        google()
    }

    dependencies {
        classpath group: 'commons-io', name: 'commons-io', version: '2.8.0'
        classpath "com.google.code.gson:gson:2.8.9"
    }
}

//*****************//
//     Plugins     //
//*****************//
plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id "org.panteleyev.jpackageplugin"
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'bubbles-gradle'

//****************************//
// Setting up main properties //
//****************************//

// Project properties.
def projectId = property("project_id")
def projectName = property("project_name")
def projectVersion = property("project_version")
def appName1 = property("app_name")

version "${project_version}+${getenv("GITHUB_BUILD_NUMBER") == null ? "local" : getenv("GITHUB_BUILD_NUMBER")}"
group 'com.ultreon.bubbles' // http://maven.apache.org/guides/mini/guide-naming-conventions.html

def packageVersion = ((String) version).replaceAll("\\+local", ".0").replaceAll("\\+", ".")

java {
    withSourcesJar()
    withJavadocJar()
}

task deleteOldJavadoc(type: Delete) {
    delete fileTree("$projectDir/docs/latest/")
}

task copyJavadoc(type: Copy) {
    from javadoc.outputs
    into "$projectDir/docs/latest/"
    dependsOn deleteOldJavadoc
}

mkdir "$projectDir/docs/"
mkdir "$projectDir/docs/latest"

javadoc.finalizedBy(copyJavadoc)

bubbles {
    versionType(VersionType.DEV)
}

//**********************//
//     Repositories     //
//**********************//
repositories {
    mavenCentral()
    mavenLocal()
    google()
    maven {
        name "JitPack"
        url 'https://jitpack.io'
    }
    maven {
        name "Atlassian"
        url "https://maven.atlassian.com/3rdparty/"
    }
    maven {
        name "ImageJ"
        url "https://maven.imagej.net/content/repositories/public/"
    }
    maven {
        name "Maven Central"
        url "https://repo1.maven.org/maven2/"
    }
    maven {
        name "RuneLite"
        url "https://repo.runelite.net/"
    }
    flatDir {
        name "Project Libraries"
        dirs "${projectDir}/libs"
    }
    jcenter()
}

/***************
 * Dependencies
 */
dependencies {
    // Projects
    implementation project(":preloader")
    implementation 'it.unimi.dsi:fastutil:8.5.9'
    implementation 'it.unimi.dsi:fastutil-extra:8.5.4'
    implementation 'it.unimi.dsi:fastutil-core:8.5.9'
    implementation 'org.checkerframework:checker:3.25.0'

    implementation 'com.github.Ultreon:ultreon-data:64728df4b2'

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    // Misc
    implementation 'org.jetbrains:annotations:23.0.0'

    // Apis
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'org.apache.ant:ant:1.10.12'

    //noinspection GradlePackageUpdate
    implementation 'commons-lang:commons-lang:2.6'

    // Local jars.
    implementation fileTree(dir: "$projectDir/libs", include: ['*.jar'])

    // Google
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'

    // Apache
    implementation group: 'org.apache.logging.log4j', name: 'log4j', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-web', version: '2.14.1'
    implementation group: 'org.apache.xmlgraphics', name: 'batik-svg-dom', version: '1.14'
    implementation group: 'org.apache.xmlgraphics', name: 'batik-dom', version: '1.14'
    implementation group: 'org.apache.xmlgraphics', name: 'batik-all', version: '1.14'
    implementation group: 'org.apache.xmlgraphics', name: 'batik-util', version: '1.14'
    implementation group: 'org.apache.xmlgraphics', name: 'batik-swing', version: '1.14'
    implementation group: 'org.apache.xmlgraphics', name: 'batik-awt-util', version: '1.14'

    // JHLabs
    implementation group: 'com.jhlabs', name: 'filters', version: '2.0.235-1'

    // SwingLabs
    implementation group: 'org.swinglabs.swingx', name: 'swingx-all', version: '1.6.5-1'

    // FuseSource
    implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.3.4'

    // OpenJFX
    implementation 'org.openjfx:javafx-swing:19'
    implementation 'org.openjfx:javafx-media:19'
    implementation 'org.openjfx:javafx-controls:19'
    implementation 'org.openjfx:javafx-graphics:19'
    implementation 'org.openjfx:javafx:19'

    // OpenJFX Natives
    implementation 'org.openjfx:javafx-swing:19:win'
    implementation 'org.openjfx:javafx-swing:19:mac'
    implementation 'org.openjfx:javafx-swing:19:linux'
    implementation 'org.openjfx:javafx-media:19:win'
    implementation 'org.openjfx:javafx-media:19:mac'
    implementation 'org.openjfx:javafx-media:19:linux'
    implementation 'org.openjfx:javafx-controls:19:win'
    implementation 'org.openjfx:javafx-controls:19:mac'
    implementation 'org.openjfx:javafx-controls:19:linux'
    implementation 'org.openjfx:javafx-graphics:19:win'
    implementation 'org.openjfx:javafx-graphics:19:mac'
    implementation 'org.openjfx:javafx-graphics:19:linux'
    implementation 'org.openjfx:javafx-base:19:win'
    implementation 'org.openjfx:javafx-base:19:mac'
    implementation 'org.openjfx:javafx-base:19:linux'

    // MongoDB
    implementation group: 'org.mongodb', name: 'bson', version: '4.3.2'

    // OSHi (For system info in crash reports)
    implementation 'com.github.oshi:oshi-core:6.3.2'

    // Discord Presence
    implementation 'com.github.JnCrMx:discord-game-sdk4j:v0.5.5'

    // MP3 Sounds
    implementation group: 'javazoom', name: 'jlayer', version: '1.0.1'

    // Apache commons
    implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
}

// task to add addons.json to all relevant folders
task replaceResources(type: Copy) {
    from("src/main/resources/META-INF/addons.json")
    outputs.upToDateWhen {
        false
    }

    filter { line -> line.replaceAll('\\\$\\\$ADDON_VERSION\\\$\\\$', version.toString()) }
    into new File(processResources.destinationDir, "META-INF")
}

processResources {
    exclude 'META-INF/addons.json'
    finalizedBy replaceResources
}

jar {
    dependsOn ":preloader:jar"

    //noinspection GroovyAssignabilityCheck
    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes 'Implementation-Title': 'Bubble Blaster 2',
                'Implementation-Vendor': 'Ultreon Team',
                'Implementation-Version': project.version,
                'Main-Class': 'com.ultreon.preloader.PreGameLoader',
                'Multi-Release': 'true'
    }

    from {
        compileJava.outputs
    }
    from {
        processResources.outputs
    }

    zip64 true
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

processResources {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

test {
    useJUnitPlatform()
}

tasks.create('prepareRun', {
    Files.createDirectories(Paths.get(projectDir.getAbsolutePath(), "run"))
})

tasks.withType(JavaCompile).configureEach {
    options.fork = true
    options.incremental = true
}

tasks.withType(ProcessResources).configureEach {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

build {
    mustRunAfter ":packedJar"
}

task createExec {
    doFirst {
        if (!file("build").exists()) {
            mkdir "build"
        }
        mkdir "build/exec/"
        if (file("build/exec/lib/").exists()) {
            delete fileTree("build/exec/lib/")
        }
        copy {
            from file("bin/start.bat")
            into file("build/exec/")
        }
        copy {
            from file("bin/start-unix.sh")
            into file("build/exec/")
        }
        copy {
            from file("bin/start-mac.sh")
            into file("build/exec/")
        }
        copy {
            from jar.outputs.files.singleFile
            into file("build/exec/")
            rename ".*", "bubbles.jar"
        }
        mkdir "build/exec/lib/"
        copy {
            from fileTree("build/dependencies/")
            into file("build/exec/lib/")
        }
    }
}

jar.finalizedBy(retrieveUrls)
publish.finalizedBy(retrieveUrls)

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

task("copyDependencies", type: Copy) {
    from(configurations.runtimeClasspath)
    into("$buildDir/jars")
}

task("copyJar", type: Copy) {
    from(tasks.jar)
    into("$buildDir/jars")
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
println("OS: " + System.getProperty("os.name") + " Version: " + System.getProperty("os.version"))

println("Current version: " + version)
println("Project: " + group + ":" + archivesBaseName)
println("Bulding a Java " + compileJava.targetCompatibility + " project...")
